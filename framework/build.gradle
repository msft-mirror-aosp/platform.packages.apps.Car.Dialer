/*
 * Copyright (C) 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'aaosApps.buildLogic'
    id 'buildSrc.jdk.jdk8'
}

android {
    namespace "com.android.car.dialer.framework"

    defaultConfig {
        minSdkVersion 31
        targetSdkVersion 33
        versionCode 1
        versionName '1.0'
    }

    signingConfigs {
        unbundled {
            storeFile file(gradle.ext.unbundledCertPath)
            storePassword 'carapps'
            keyAlias 'carapps'
            keyPassword 'carapps'
        }
    }

    flavorDimensions 'devMode'
    productFlavors {
        fake {
            dimension 'devMode'
            signingConfig signingConfigs.unbundled
        }

        emulator {
            dimension 'devMode'
            signingConfig signingConfigs.unbundled
        }

        production {
            dimension 'devMode'
            signingConfig signingConfigs.unbundled
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src']
            manifest.srcFile 'real/AndroidManifest.xml'
        }

        production {
            java.srcDirs = ['real/src']
        }

        emulator {
            java.srcDirs = ['real/src']
        }

        fake {
            java.srcDirs = ['fake/src']
            res.srcDirs = ['fake/res']
            manifest.srcFile 'fake/AndroidManifest.xml'
            assets.srcDirs = ['fake/assets']
        }
    }
}

dependencies {
    implementation project(':car-apps-common')
    implementation project(':car-telephony-common')
    implementation files(gradle.ext.lib_car_system_stubs)
    implementation 'com.google.dagger:hilt-android:2.38.1'
    annotationProcessor 'com.google.dagger:hilt-android-compiler:2.38.1'

    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.core:core:1.6.0'

    fakeRuntimeOnly 'androidx.legacy:legacy-support-v4:1.0.0'
    fakeImplementation 'com.google.code.gson:gson:2.8.8'
    fakeImplementation 'com.google.guava:guava:30.1.1-android'
    fakeImplementation libs.dexmaker.mockito.inline
}
